# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem              1.0

name                    php54
set php                 ${name}
# Increment revision of ${php}-eaccelerator when updating version of ${php}.
version                 5.4.0RC8
set major               [lindex [split ${version} .] 0]
set suffix              [join [lrange [split ${version} .] 0 1] {}]
categories              lang php www
maintainers             ryandesign jwa
platforms               darwin freebsd
license                 PHP-3.01
use_parallel_build      yes

description             PHP: Hypertext Preprocessor

long_description        PHP is a widely-used general-purpose scripting \
                        language that is especially suited for developing \
                        web sites, but can also be used for command-line \
                        scripting.

homepage                http://qa.php.net/
master_sites            http://downloads.php.net/stas/

dist_subdir             php${major}
distname                php-${version}
use_bzip2               yes

checksums               [suffix ${distname}] \
                        rmd160  618b9526094c2e7375b272f69779335dfdb68946 \
                        sha256  77ee8ae882a38ad31c9a034b90fa635c417853c99686e7f36887d381a5f73806

if {${configure.compiler} == "clang"} {
    configure.compiler llvm-gcc-4.2
}

if {${subport} == "${php}" || ${subport} == "${php}-apache2handler" || ${subport} == "${php}-cgi" || ${subport} == "${php}-fpm"} {
    
    ### SAPIs ###
    
    depends_build       port:pkgconfig
    
    depends_lib         path:bin/gsed:gsed \
                        port:libiconv \
                        port:libtool \
                        port:libxml2 \
                        port:bzip2 \
                        port:mhash \
                        port:pcre \
                        port:readline \
                        port:zlib
    
    # Use -p1 to accommodate the Suhosin patch
    patch.pre_args      -p1
    patchfiles          patch-scripts-php-config.in.diff
    
    use_autoconf        yes
    
    set phpinidir       ${prefix}/etc/${php}
    set extraphpinidir  ${prefix}/var/db/${php}
    
    configure.args      --mandir=${prefix}/share/man \
                        --infodir=${prefix}/share/info \
                        --program-suffix=${suffix} \
                        --includedir=${prefix}/include/${php} \
                        --libdir=${prefix}/lib/${php} \
                        --with-config-file-path=${phpinidir} \
                        --with-config-file-scan-dir=${extraphpinidir} \
                        --disable-all \
                        --enable-bcmath \
                        --enable-ctype \
                        --enable-dom \
                        --enable-fileinfo \
                        --enable-filter \
                        --enable-hash \
                        --enable-json \
                        --enable-libxml \
                        --enable-pdo \
                        --enable-phar \
                        --enable-session \
                        --enable-simplexml \
                        --enable-tokenizer \
                        --enable-xml \
                        --enable-xmlreader \
                        --enable-xmlwriter \
                        --with-bz2=${prefix} \
                        --with-mhash=${prefix} \
                        --with-pcre-regex=${prefix} \
                        --with-readline=${prefix} \
                        --with-libxml-dir=${prefix} \
                        --with-zlib=${prefix} \
                        --without-pear \
                        --disable-cgi \
                        --disable-cli \
                        --disable-fpm
    
    # ${php}-mysql +mysqlnd needs mysqlnd support compiled into the SAPI
    configure.env       PHP_MYSQLND_ENABLED=yes
    
    configure.universal_args-delete --disable-dependency-tracking
    
    test.run            yes
    
    destroot.args       INSTALL_ROOT=${destroot}
    
    variant debug description {Enable debug support (useful to analyze a PHP-related core dump)} {
        configure.args-append   --enable-debug
    }
    
    variant suhosin description {Add Suhosin patch} {
        pre-fetch {
            if {"darwin" == ${os.platform} && ${os.major} < 9} {
                ui_error "The suhosin variant requires Mac OS X 10.5 or greater."
                return -code error "incompatible Mac OS X version"
            }
            set suhosin_available 0
            if {!${suhosin_available}} {
                ui_error "There is no suhosin patch for PHP ${version} yet. Please check back later."
            }
            if {![file exists ${extraphpinidir}/suhosin.ini]} {
                ui_msg "You may also be interested in the suhosin extension, a related but different piece of software. See the ${php}-suhosin port."
            }
            if {!${suhosin_available}} {
                return -code error "unavailable variant"
            }
        }
        set suhosin_patch_version   5.3.7-0.9.10
        set suhosin_patch           suhosin-patch-${suhosin_patch_version}.patch.gz
        patch_sites-append          http://download.suhosin.org/
        patchfiles-append           ${suhosin_patch}
        checksums-append            ${suhosin_patch} \
                                    rmd160  19f789bf49a5fed2cd88b199fd8ac5d1ffa9bdc8 \
                                    sha256  0a0dac0e4343596f2bfcef27dcf7180524b78bb1d2d2ca878370a7d0f8313d26 \
    }
    
    if {${subport} != ${php}} {
        notes-append "If this is your first install, you need to enable ${subport} in your web server."
    }
}

if {${subport} == ${php}} {
    configure.args-delete   --disable-cli
    configure.args-append   --enable-cli
    
    destroot.target         install-cli install-build install-headers install-programs
    
    destroot.keepdirs       ${destroot}${extraphpinidir}
    
    post-destroot {
        # Copy the default php.ini files.
        xinstall -m 755 -d ${destroot}${phpinidir}
        xinstall -m 644 -W ${worksrcpath} \
            php.ini-development \
            php.ini-production \
            ${destroot}${phpinidir}
        
        # Copy mysqlnd headers.
        xinstall -d ${destroot}${prefix}/include/${php}/php/ext/mysqlnd
        eval xinstall -m 644 [glob ${worksrcpath}/ext/mysqlnd/*.h] ${destroot}${prefix}/include/${php}/php/ext/mysqlnd
    }
    
    if {![file exists ${phpinidir}/php.ini]} {
        notes-append "
To customize ${php}, copy\
${phpinidir}/php.ini-development (if this is a development server) or\
${phpinidir}/php.ini-production (if this is a production server) to\
${phpinidir}/php.ini and then make changes.
"
    } else {
        notes-append "
You may need to update your php.ini for any changes that have been made\
in this version of ${php}. Compare ${phpinidir}/php.ini with\
${phpinidir}/php.ini-development (if this is a development server) or\
${phpinidir}/php.ini-production (if this is a production server).
"
    }
    
    livecheck.type          regex
    livecheck.url           ${homepage}
    livecheck.regex         php-?(5\\.\[0-9.\]+(?:(?:alpha|beta|RC)\\d+|-latest))\\.tar
} else {
    depends_lib-append      port:${php}
    
    livecheck.type          none
}

subport ${php}-apache2handler {
    description             ${php} Apache 2 Handler SAPI
    
    long_description        ${description}
    
    depends_lib-append      port:apache2
    
    set apxs ${prefix}/apache2/bin/apxs
    set confdir ${prefix}/apache2/conf
    set moduledir ${prefix}/apache2/modules
    
    pre-configure {
        # Checking for mod_cgi.so is a convenient way to verify apache2 is using its
        # +preforkmpm variant. (+eventmpm and +workermpm instead provide mod_cgid.so.)
        if {![file exists ${moduledir}/mod_cgi.so]} {
            ui_error "To use ${subport}, apache2 must be installed with the +preforkmpm variant."
            return -code error "incompatible apache2 installation"
        }
    }
    
    configure.args-append   --with-apxs2=${apxs}
    
    build.target            libs/libphp5.bundle
    
    destroot.violate_mtree  yes
    
    destroot {
        xinstall -m 755 -d ${destroot}${moduledir} ${destroot}${confdir}/extra
        xinstall -m 644 ${worksrcpath}/libs/libphp5.so ${destroot}${moduledir}/mod_${php}.so
        xinstall -m 644 ${filespath}/mod_php.conf ${destroot}${confdir}/extra/mod_${php}.conf
    }
    
    notes-append "

To enable ${subport}, run:

    cd ${moduledir}
    sudo ${apxs} -a -e -n php${major} mod_${php}.so
"
}

subport ${php}-cgi {
    description             ${php} CGI SAPI
    
    long_description        ${description}
    
    configure.args-delete   --disable-cgi
    configure.args-append   --enable-cgi
    
    build.target            cgi
    
    destroot.target         install-cgi
}

subport ${php}-fpm {
    description             ${php} FPM SAPI
    
    long_description        ${description}
    
    set fpmuser             nobody
    set fpmgroup            nobody
    
    patchfiles-append       patch-sapi-fpm-php-fpm.conf.in.diff
    
    post-patch {
        reinplace "s|@PHP@|${php}|g" ${worksrcpath}/sapi/fpm/php-fpm.conf.in
    }
    
    configure.args-delete   --disable-fpm
    configure.args-append   --enable-fpm \
                            --sysconfdir=${phpinidir} \
                            --with-fpm-user=${fpmuser} \
                            --with-fpm-group=${fpmgroup}
    
    build.target            fpm
    
    destroot.target         install-fpm
    
    destroot.keepdirs       ${destroot}${prefix}/var/log/${php} \
                            ${destroot}${prefix}/var/run/${php}
    
    post-destroot {
        xinstall -d -o ${fpmuser} -g ${fpmgroup} ${destroot}${prefix}/var/log/${php} ${destroot}${prefix}/var/run/${php}
    }
    
    startupitem.create      yes
    startupitem.executable  ${prefix}/sbin/php-fpm${suffix}
    
    if {![file exists ${phpinidir}/php-fpm.conf]} {
        notes-append "

To use ${subport}, copy\
${phpinidir}/php-fpm.conf.default to\
${phpinidir}/php-fpm.conf and make changes if desired.
"
    } else {
        notes-append "

You may need to update your php-fpm.conf for any changes that have been made\
in this version of ${subport}. Compare ${phpinidir}/php-fpm.conf with\
${phpinidir}/php-fpm.conf.default.
"
    }
}
